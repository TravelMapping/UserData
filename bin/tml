#!/bin/csh
#
# Script to move one or more TM list files to the list_files directory,
# correcting for any common problems with file extensions.
#
if ( $#argv < 1 ) then
  echo "Usage: $0 listfilenames"
  exit 0
endif

set listdir=~/travelmapping/UserData/list_files
set rlistdir=~/travelmapping/UserData/rlist_files

# counters for the number of list files and rlist files moved, and the number of each
# that are new files
set listfiles=0
set rlistfiles=0
set newlistfiles=0
set newrlistfiles=0

# loop over all command line parameters
foreach arg ( $* )
  if ( -f ${arg} ) then
    # try for common file extension formats and fix as needed
    set username=`echo ${arg} | cut -d'.' -f1`
    if ( ${arg} == ${username}.list ) then
      set destfilename=${arg}
      set dest=${listdir}
    else if ( ${arg} == ${username}.list.txt ) then
      set destfilename=${username}.list
      set dest=${listdir}
    else if ( ${arg} == ${username}.txt ) then
      set destfilename=${username}.list
      set dest=${listdir}
    else if ( ${arg} == ${username}.rlist ) then
      set destfilename=${arg}
      set dest=${rlistdir}
    else if ( ${arg} == ${username}.rlist.txt ) then
      set destfilename=${username}.rlist
      set dest=${rlistdir}
    else
      echo "Unexpected extension on ${arg}, aborting!"
      exit 1
    endif

    echo "Moving ${arg} as ${destfilename} to ${dest}"
    chmod 644 ${arg}
    if ( ${dest} == ${listdir} ) then
      @ listfiles++
      if ( ! -f ${dest}/${destfilename} ) then
        @ newlistfiles++
      endif
    else if ( ${dest} == ${rlistdir} ) then
      @ rlistfiles++
      if ( ! -f ${dest}/${destfilename} ) then
        @ newrlistfiles++
      endif
    endif
    if ( ! -f ${dest}/${destfilename} ) then
      echo "NOTE: this appears to be a new user list file, remember to run:"
      echo "git add ${dest}/${destfilename}"
      echo 'git commit -m "New user list submitted by email."'
    endif
    mv ${arg} ${dest}/${destfilename}
  else
    echo "ERROR: no file ${arg}, ignoring"
  endif
end

# print summary of what was done
echo "Moved ${listfiles} list files, ${newlistfiles} of which are new."
echo "Moved ${rlistfiles} rlist files, ${newrlistfiles} of which are new."
